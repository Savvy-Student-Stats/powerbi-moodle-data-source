// AssignmentStat
// using reference (`Source = Assignment`) b/c a join was grossly complicated, thus slow, looking.
// 100+ lines of code down to 7 this way.
// @since 2021-02-08
//
// Converting dates here instead of on Assignment query, but that may change.
// maybe rename that AssignmentSource and then this becomes Assignment or StatsAssignment?

let
    Source = Assignment,

    DEFAULT_DATE = DateTime.Date(2019, 1, 1),
    EPOCH = #datetime(1970, 1, 1, 0, 0, 0),

    TimestampToDate = (timestamp as number) =>
        if timestamp = 0
            then DEFAULT_DATE
            else DateTime.Date(EPOCH + #duration(0, 0, 0, timestamp))
    ,

    // thanks https://stackoverflow.com/a/38275848/269078
    ConvertTimestampColumns = (sourceTable as table, colNames as list) =>
        List.Accumulate(
            colNames,
            sourceTable,
            (state, colName) =>
                Table.AddColumn(
                    state,
                    colName & "_asDate",
                    // "you can use the underscore „_“ to reference the current row of the table, which means the current record."
                    each TimestampToDate(Record.Field(_, colName)),
                    type date
                )
        )
    // end function
    ,
    //



    // Convert Moodle timestamps to date
    #"Converted Timestamps" = ConvertTimestampColumns(
        Source,
        {
            "duedate",
            "allowsubmissionsfromdate",
            "timemodified",
            "gradingduedate"
        }
    )
in
    #"Converted Timestamps"
